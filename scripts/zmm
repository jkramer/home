#!/usr/bin/env zsh
# ZMM (ZSH Music Menu)
# Copyright (C) 2009-2010 by Jonas Kramer. All rights reserved.
# Published under the terms of the GNU General Public License (GPL).

typeset -A command_translation
typeset -a command_list


# Try to detect which player is running.

# If $SHC_SOCKET is set and ppints to an existing file, assume that shell-fm is
# running.
if [[ ! -z $SHC_SOCKET && -e $SHC_SOCKET ]]; then
	CTL="shell-fm-command"

	shell-fm-command () {
		COMMAND="$@"

		zmodload 'zsh/net/socket'
		if ! zsocket $SHC_SOCKET; then
			print -u 2 "Socket connection failed. $!."
			exit -1
		fi

		print -u $REPLY "$COMMAND"
	}

	command_list=(next love ban pause stop quit)

# Otherwise check if mpc is able to get the version from mpd. If so, it's
# likely to be running...
elif mpc version &>"/dev/null"; then
	CTL="mpc-ns"

	mpc-ns () {
		mpc --no-status $@
	}

	command_list=(toggle previous next pause stop delete)

	command_translation[previous]="prev"
	command_translation[delete]="del 0"

# If no running player is found, there's nothing to do for us.
# TODO: Use dmenu to let the user choose a player to start.
else
	exit
fi

print -l $command_list | dmenu $@ | {
    read choice

    if [[ ! -z $choice ]]; then
        if [[ ! -z $command_translation[$choice] ]]; then
            choice=$command_translation[$choice]
        fi

        $CTL ${(s: :)choice}
    fi
}
